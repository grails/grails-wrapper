publishing {
    publications {
        maven(MavenPublication) {
            pom.withXml {
                def xml = asNode()
                xml.children().last() + pomInfo
                try {
                    xml.dependencyManagement.replaceNode {}
                } catch (Throwable e) {
                    // ignore
                }

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                xml.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    try {
                        it.replaceNode {}
                    } catch (Throwable e) {
                        // ignore
                    }
                }
            }
            artifactId project.name
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        if (isSnapshot) {
            maven {
                credentials {
                    def u = System.getenv("ARTIFACTORY_USERNAME") ?: project.hasProperty("artifactoryPublishUsername") ? project.artifactoryPublishUsername : ''
                    def p = System.getenv("ARTIFACTORY_PASSWORD") ?: project.hasProperty("artifactoryPublishPassword") ? project.artifactoryPublishPassword : ''
                    username = u
                    password = p
                }
                url "https://repo.grails.org/grails/libs-snapshots-local"
            }
        }
    }
}

task install(dependsOn: project.tasks.withType(PublishToMavenLocal))
